// Quest Alexandría. Inicio de Instancia Terminus.

// IDs reservadas Quest Alexandria / Terminus
//60500 - 60600

// IDs reservadas Items de Quest Alexandria / Terminus
//30250 - 30275

// Variable personal para avance de quest: alexa_q

//falta:
//scripts para el registro de party (linea 337)

prontera,164,171,5	script	Ansem#alexa	740,{ //plaza de prontera norte
    
    set .@name$,"[ ^FF0000Ansem^000000 ]";
    if(alexa_q == 0) {
        mes .@name$;    
        mes "Esto no puede seguir así...";
        next;
        mes "[^00ff00"+strcharinfo(0)+"^000000]";    
        mes "¿Sucede algo? ¿Puedo ayudar?";
        next;
        mes .@name$;
        mes "No lo creo joven, es más, yo que tu tendría cuidado y no saldría a la calle por las noches. Están habiendo ataques.";  
        next;
        mes "[^00ff00"+strcharinfo(0)+"^000000]";    
        mes "¿Ataques? ¿De quién?";
        next;    
        mes .@name$;
        mes "¡Sombras! O al menos asi lo describen los que han sobrevivido a los ataques. Han muerto varias personas ya...";
        next;    
        mes .@name$;
        mes "Me han enviado aquí para investigarlo y encontrar al asesino. Pero no tengo por donde empezar. Visite ayer a los supervivientes, pero el único que ha despertado se encontraba en estado de shock. Pobre niña... Ni siquiera habla con sus amigos del colegio.";
        set alexa_q,1;
        close;    
    }
    else if(alexa_q <= 2) {
        mes .@name$;
        mes "Ten cuidado por las noches, parece que es cuando atacan... *Suspiro* Debería ver a esa pequeña de nuevo.";   
        close;   
    }
    else if(alexa_q == 3) {
        mes .@name$;    
        mes "¿Cómo? ¡¿Que has conseguido hablar con la niña?!";
        next;
        mes .@name$;    
        mes "Ya veo, asi que eso sucedió... Quizá si puedas servirme de ayuda. Esas \"chispas\" de las que habló, segúramente sean algún tipo de energía. Si tuviera los materiales, quizá podría construir un dispositivo para rastrear esa energia y descubrir donde se esconden esas sombras. ¿Qué dices, me ayudarás?";
        switch(select("¡Claro! ¿Qué necesitas?:Déjame que lo piense...")) {
            case 1:
                mes .@name$;    
                mes "Vamos a ver... Necesito:";
                mes "250 ^008000Used Iron Piece^000000";            
                mes "100 ^008000Solid Iron Plate^000000";            
                mes "25 ^008000Fragment^000000";            
                mes "y 20 ^008000Crystal Fragments^000000 servirán como alimentación.";
                next;    
                mes .@name$;
                mes "Empezaré a construirlo en cuanto me traigas todo.";
                set alexa_q,4;
                close;
            case 2:
                mes .@name$;    
                mes "Está bien. Vuelve cuando estés preparado.";            
                close;
        }    
    }
    else if(alexa_q == 4) {
        mes .@name$;
        mes "¿Ya estás de vuelta? ¿Lo tienes todo? Déjame ver:";
        next;
        if(countitem(7319) < 250 || countitem(7507) < 100 || countitem(7094) < 25 || countitem(7321) < 20) {
            mes .@name$;
            mes "Esto no es suficiente para el dispositivo, vuelve cuando tengas todo, por favor.";
            next;
            mes .@name$;
            mes "Recuerda, necesito lo siguiente:";
            mes "250 ^008000Used Iron Piece^000000";            
            mes "100 ^008000Solid Iron Plate^000000";            
            mes "25 ^008000Fragment^000000";            
            mes "y 20 ^008000Crystal Fragments^000000.";
            close;
        }    
        else {
            mes .@name$;
            mes "¡Bien, lo tienes todo!";
            //next; Eliminación del next para evitar bugs con el delitem. (Siempre IF (ver si tiene) delitem.)
            mes "Empezaré ya mismo, segúramente termine el dispositivo para mañana. Ven a verme ^ff0000mañana^000000 y empezaremos a rastrear la zona.";
            delitem 7319,250;
            delitem 7507,100;
            delitem 7094,25;
            delitem 7321,20;
            set alexa_q,5;
            set alexaq_time, gettimetick(2) + 64800;    
            close;
        }
    }
    else if(alexa_q == 5) {
        if(alexaq_time > gettimetick(2)){
            mes .@name$;
            mes "Lo siento, aún no he terminado el dispositivo. Vuelve más tarde.";
            close;
        }
        else
        {
            mes .@name$;
            mes "Hola "+strcharinfo(0)+", me alegro de que estes de vuelta. Ya he terminado el dispositivo, ¿te apetece probarlo?";
            next;    
            mes .@name$;
            mes "Cuando active el dispositivo, te marcará en el mapa las zonas con energía. Solo debes acercarte a esas zonas e investigar. Si encuentras algo raro o quieres que te vuelva a marcar las zonas, ven a verme.";
            set alexa_q,6;
            viewpoint 1,148,283,1,0xFF0000; //plaza norte
            viewpoint 1,122,91,2,0xFF0000; //floristeria suroeste
            viewpoint 1,194,300,3,0xFF0000; //iglesia y sombra
            viewpoint 1,259,67,4,0xFF0000; //muñecos de entrenamiento
            close;
        }
    }
    else if(alexa_q == 6) {
        mes .@name$;
        mes "Te volveré a marcar las zonas. Avísame si encuentras algo.";
        viewpoint 1,148,283,1,0xFF0000; //plaza norte
        viewpoint 1,122,91,2,0xFF0000; //floristeria suroeste
        viewpoint 1,194,300,3,0xFF0000; //iglesia y sombra
        viewpoint 1,259,67,4,0xFF0000; //muñecos de entrenamiento
        close;
    }
    else if(alexa_q == 7) {
        mes .@name$;
        mes "¿Qué es eso? ¿Dices que lo tenia la sombra? Déjame examinarlo...";
        next;
        mes .@name$;
        mes "...";
        next;
        mes .@name$;
        mes "...";
        mes "...";
        next;
        mes .@name$;
        mes "...";
        mes "...";
        mes "Imposible...";
        mes "¡Esto es de Alexandría!";
        next;
        mes .@name$;
        mes "Estos cristales son usadas en esa cuidad como fuente de energia para algunas maquinas, algunos los llaman Cristales de Alma.";
        next;
        mes .@name$;
        mes "¿Acaso estás sombras tienen relación con Alexandría?";
        next;
        mes .@name$;
        mes "...";
        next;
        mes .@name$;
        mes "...";
        mes "...";
        next;
        mes .@name$;
        mes "...";
        mes "...";
        mes "¡Decidido! Nos vamos a Alexandría.";
        next;
        mes .@name$;
        mes "Habla conmigo cuando estés preparado para partir, necesitaré tu ayuda.";
        set alexa_q,8;
        close;
    }
    else if(alexa_q == 8) {
        mes .@name$;
        mes "¿Estás listo para ir a Alexandría?";
        switch(select("¡Estoy listo!:Aún no.")) {
            case 1:
                mes .@name$;
                mes "Esos cristales son sacados de una cueva de la zona. Cuando lleguemos allí, habla con la gente del puerto, puede que sepa algo. Yo preguntaré sobre la cueva. Si averiguas algo, te estaré esperando en la entrada de la cueva."; 
                next;
                mes .@name$;
                mes "¡Muy bien, allá vamos!";
                set alexa_q,9;
                next;
                close2;
                warp alexa_field,49,129;
                end;
            case 2:
                mes .@name$;
                mes "Ven a verme cuando estés preparado para partir.";
                close;
        }
    }
    else if(alexa_q > 8){  // Nosé que sigue de la quest pero por siaca.
        mes .@name$;
        mes "¿Quieres que te lleve a Alexandria?";
        switch(select("¡Por supuesto!:No, gracias.")) {       
            case 1:
                mes .@name$;
                mes "¡Muy bien, allá vamos!";
                set alexa_q,9;
                next;
                close2;
                warp alexa_field,49,129;
                end;
           case 2:     
                mes .@name$;
                mes "Ven a verme si cambias de opinión.";        
                close;
        }        
    }
}

prt_in,43,112,5	script	Estudiante#alexa 706,{ //escuela de prontera
    
    set .@name$,"[ ^FF0000Estudiante^000000 ]";

    if(alexa_q == 1) {
        mes .@name$;
        mes "¿Atacada? ¿Te refieres a Helen? La hemos visitado, pero no nos hablaba, solo decia cosas raras de una sombra. Si quieres visitarla, está en la posada.";
        set alexa_q,2;
        close;
    }
    else {
        mes .@name$;
        mes "Helen ha dejado de venir a clase. La echo de menos.";    
        close;
    }
}

prt_in,60,170,5	script	Helen#alexa	72,{ //posada del centro de prontera

    set .@name$,"[ ^FF0000Helen^000000 ]";
    set .@name_char$,"[^00ff00"+strcharinfo(0)+"^000000]";
    if(alexa_q < 2) {
        mes .@name$;
        mes "....";    
        next;    
        mes .@name$;
        mes "....";    
        mes "-No parece que vaya a hablarte-";   
        close;
    }
    else if(alexa_q == 2) {
        mes .@name$;
        mes "...";
        next;
        mes .@name$;
        mes "...";    
        mes "¿Quién eres?";
        next;
        mes .@name_char$;
        mes "Me llamo "+strcharinfo(0)+" Disculpa, ¿eres tú aquella a la que atacaron?";
        next;
        mes .@name$;
        mes "...si.";    
        next;
        mes .@name_char$;
        mes "¿Podrías contarme lo que sucedió?";
        next;
        mes .@name$;
        mes "...solo recuerdo partes. Era de noche y estaba muy oscuro. Iba por la calle de la iglesia cuando escuche un sonido extraño detras mia. Me di la vuelta y... y..."; 
        next;
        mes .@name_char$;
        mes "¿Qué pasó?";
        next;
        mes .@name$;
        mes "Había una sombra ahi. Lo único que recuerdo es que se avalanzó sobre mí... después desperte en casa.";    
        next;
        mes .@name_char$;
        mes "Ya veo... ¿No recuerdas nada más?";
        next;
        mes .@name$;
        mes "Recuerdo las chispas que le salian del suelo... Y Esos ojos... me dan miedo solo recordarlos. ¡Sé que volverá!";
        mes "-Notas como de sus ojos sale una lágrima-";
        next;
        mes .@name_char$;
        mes "Tranquila, no te preocupes. Encontraré esa sombra.";
        next;
        mes .@name$;
        mes "¿D-de verdad lo harás? ¡Gracias!";
        next;
        mes "-Será mejor contarle esto a Ansem, quizá él también haya averiguado algo-";
        set alexa_q,3;
        close;
    }    
    else if(alexa_q > 2 && alexa_q < 11){
        mes .@name$;
        mes "Por favor, encuéntrala. Tengo miedo de salir a la calle, no quiero verla de nuevo."; 
        close;
    }            
    else if(alexa_q >= 11) { 
        mes .@name$;
        mes "Hecho de menos a mis amigos del colegio..."; 
        close;    
    }
}

prontera,148,283,5	script	marca1#alexa::marca1	844,{ //plaza norte
    end;
OnInIt:
    disablenpc "marca1";
    end;

OnTouch:
    if(alexa_q == 6) {
        mes "El dispositivo detecta una señal muy débil, será mejor buscar en otro lado.";
        viewpoint 2,148,283,1,0xFF0000; //plaza norte
        close;
    }
    end;
}

prontera,122,91,5	script	marca2#alexa::marca2	844,{ //floristeria suroeste
    end;

OnInIt:
    disablenpc "marca2";
    end;
    
OnTouch:
    if(alexa_q == 6) {
        mes "El dispositivo detecta una señal muy débil, será mejor buscar en otro lado.";
        viewpoint 2,40,50,2,0xFF0000; //floristeria suroeste
        close;
    }   
    end;
}

prontera,194,300,5	script	marca1#alexa::marca3	2955,{ //iglesia y sombra
    end;
OnInIt:
    disablenpc "marca3";
    end;
    
OnTouch:
    if(alexa_q == 6) {
        mes "¡Parece que el dispositivo a detectado algo!";
        viewpoint 2,194,300,3,0xFF0000; //iglesia y sombra
        next;
        mes "De repente notas una presencia extraña...";
        enablenpc "marca3";
        next;
        mes "Eso es... ¡¿Una sombra?!";
        select("Enfrentarla.:Huir");
        disablenpc "marca3"; 
        mes "Antes de que puedas hacer nada, la sombra desaparece, dejando caer algo.";
        next;
        mes "Decides recoger el objeto, una especie de cristal oscuro, y llevárselo a Ansem.";
        set alexa_q,7;    
        close;
        }
    end;

}

prontera,259,67,5	script	marca4#alexa::marca4	844,{ //muñecos de entrenamiento
    end;
OnInIt:
    disablenpc "marca4";
    end;
    
OnTouch:
    if(alexa_q == 6) {
        mes "El dispositivo detecta una señal muy débil, será mejor buscar en otro lado.";
        viewpoint 2,259,67,4,0xFF0000; //muñecos de entrenamiento
        close;
    }
    end;
}

alexa_field,227,230,5	script	Ansem#alexa	740,{ //entrada dungeon

    set .@name$,"[ ^FF0000Ansem^000000 ]";
    if(alexa_q < 9){
       mes .@name$;
       mes ". . .";
       close;
    }
    else if(alexa_q == 9) {
        mes "¡Aquí está! Esta es la cueva de la que procede el Cristal de Alma. ¿Has hablado con la gente del puerto? ¡Necesitamos mas información!";
        close;
    }    
    else if(alexa_q == 10) {
        viewpoint 2,236,231,1,0xFF0000; //entrada dungeon
        mes .@name$;
        mes "¿Qué has descubierto?";
        next;
        mes .@name$;
        mes "Ya veo... ^FF0000Soul Eaters^000000. Bien, creo que no hace falta que te diga que debemos hacer.";
        next;
        mes .@name$;
        mes "Terminus está plagado de esas criaturas, es su nido. Si destruimos a su lider, acabaremos con los ^FF0000Soul Eaters^000000.";
        next;
        mes .@name$;
        mes "Esto no será fácil. Reune a tantos amigos como puedas y traelos. Vamos a entrar y luchar. Ven a verme cuando estés preparado.";
        set alexa_q,11;
        close;
    }
    //if(alexa_q == 11) {
    //REGISTRO DE PARTY
    //}
}

alexa_field,156,118,5	script	Pescador#alexa	709,{ //centro del field, frente a la puerta de una cabaña

    set .@name$,"[ ^FF0000Pescador^000000 ]";    
    if(alexa_q == 9) {
        mes .@name$;
        mes "¿Sombras? Aquí llamamos a esas criaturas ^FF0000Soul Eaters^000000. Salen sobretodo por las noches y se llevan el alma de todo aquel que pillan desprevenido. Uff... se llevaron a mi hermano la semana pasada. Era soldado y se ofreció para patrullar la zona por las noches.";
        next;
        mes .@name$;        
        mes "No hay noche que no se escuchen los rugidos de esas bestias... ¿Quien iba a decir que se esconderían en aquella caverna?";
        select("¿Qué caverna?");
        mes .@name$;
        mes "Terminus. Son unas ruinas de las que extraiamos Cristales de Alma. Un dia unos mineros hallaron una caverna repleta de estos cristales, y con ellos, las bestias. Desde entonces han estado atormentandonos.";
        next;
        mes .@name$;
        mes "Si vas a explorarla, ten cuidado. Los Soul Eaters no son como cualquier otra bestia.";
        select("¿Dónde está la caverna?");
        mes .@name$;
        mes "Te la marcaré en el mapa.";
        viewpoint 1,236,231,1,0xFF0000; //entrada dungeon
        next;
        mes .@name$;
        mes "Ten mucho cuidado, joven.";
        set alexa_q,10;
        close;
    }
    else {
        mes .@name$;
        mes "Hermano, descansa en paz.";
        close;
    }    
}
